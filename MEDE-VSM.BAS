DECLARE FUNCTION APROX (X, N)
DECLARE FUNCTION TO360 (ANG)
DECLARE FUNCTION TXT$ (N)
DECLARE FUNCTION XPIX (N)
DECLARE FUNCTION YPIX (N)
DECLARE SUB DIMTOXYZ (DEC, INC, M, X, Y, Z)
DECLARE SUB XYZTODIM (X, Y, Z, DEC, INC, M)
DECLARE SUB GETHALL ()
DECLARE SUB LEHALL ()
DECLARE SUB LESINAL ()
DECLARE SUB SETFINO (FINO)
DECLARE SUB SETGROSSO (GROSSO)
DECLARE SUB SETZERO ()
DECLARE SUB PLOTSINAL ()
DECLARE SUB PLOTHM ()
CONST PI = 3.141592653589#, GR = PI / 180, RG = 180 / PI
CONST PIT = &H40, PPI = &H61, PIO48 = &H300
CONST COUNT0 = PIT + 0, COUNT1 = PIT + 1, COUNT2 = PIT + 2, PROGPIT = PIT + 3
CONST A1 = PIO48 + 0, B1 = PIO48 + 1, C1 = PIO48 + 2, PROG1 = PIO48 + 3
CONST A2 = PIO48 + 4, B2 = PIO48 + 5, C2 = PIO48 + 6, PROG2 = PIO48 + 7
OUT PROG1, &H92 'A1=in, B1=in, C1=out
OUT PROG2, &H80 'A2=out,B2=out,C2=out
CONST NVIB = 256, FATIAS = 16, OTIMA = 50, ETMAX = 20
DIM SHARED VIBR(FATIAS), SENO(FATIAS), COSSENO(FATIAS), SINAL(NVIB, FATIAS)
DIM SHARED HALL, ERRHALL, DHALL, HALL2mT, mT2GROSSO, mT2GROSSO2, mT2FINO, CAMPO, DCAMPO
DIM SHARED ZERO, AMPL, DAMPL, FASE, DFASE, FASEPOS, FASENEG, AMPLTOT, SINAL2uAm2, MAG, DMAG
DIM SHARED ATENU, PORTAMO, ETAPA(ETMAX), MPA(3 * ETMAX), MRPA(3 * ETMAX)
DIM SHARED H(5, ETMAX), DH(5, ETMAX), M(5, ETMAX), DM(5, ETMAX)
DIM SHARED HR(5, ETMAX), DHR(5, ETMAX), MR(5, ETMAX), DMR(5, ETMAX)
DIM SHARED HMAX, HMIN, MMAX, MMIN, TRECHO, NETAP
FOR I = 1 TO FATIAS
 J = (I + FATIAS \ 2) * 2 * PI / FATIAS: VIBR(I) = INT(120 * COS(J) + .5) + 120
 K = (I - 1) * 2 * PI / FATIAS: SENO(I) = SIN(K): COSSENO(I) = COS(K)
NEXT
HALL2mT = (3.274 / 32768) * 1008 * 1224 / 3123 '(?)
SUSCAL = 5.17E-10 'm3/g
FASENEG = 90: FASEPOS = 270
mT2FINO = 3200: mT2GROSSO = 18.3: SINAL2uAm2 = .043 'provisorios

INIC:
PORTAMO = 0: ATENU = 1
CALL SETFINO(0): CALL SETGROSSO(0)
SCREEN 12: CLS : PRINT TAB(12); "PROGRAMA DE CONTROLE DO MAGNETOMETRO DE VIBRACAO (VSM)"
LOCATE 3, 1: PRINT "Colecao:                   ";
LOCATE 4, 1: PRINT "Amostra:                   ";
LOCATE 5, 1: PRINT "Massa [g]:                 ";
LOCATE 7, 1: PRINT "Porta-amostra:             ";
LOCATE 9, 1: PRINT "Atenuacao:                 ";
LOCATE 11, 1: PRINT "Trecho:                    ";
LOCATE 12, 1: PRINT "H [mT]:                    ";
LOCATE 13, 1: PRINT "M [uAm2]:                  ";
C0 = 1: CF = 27: X0 = 8 * C0 - 4: XF = 8 * CF - 4: XC = (XF + X0) / 2
L0 = 15: LF = 29: Y0 = 16 * L0 - 8: YF = 16 * LF - 8: YC = (YF + Y0) / 2
VIEW (X0, Y0)-(XF, YF), , 15: CLS
LINE (0, YC - Y0)-(XF - X0, YC - Y0)
LOCATE L0, C0 + 1: PRINT "Sinal da Magnetizacao (M)"
C0 = 28: CF = 80: X0 = 8 * C0 - 4: XF = 8 * CF - 4: XC = (XF + X0) / 2
L0 = 2: LF = 29: Y0 = 16 * L0 - 8: YF = 16 * LF - 8: YC = (YF + Y0) / 2
VIEW (X0, Y0)-(XF, YF), , 15: CLS
LINE (0, YC - Y0)-(XF - X0, YC - Y0): LINE (XC - X0, 0)-(XC - X0, YF - Y0)
LOCATE L0, C0 + 8: PRINT "Campo Aplicado (H) x Magnetizacao (M)"
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "OBS:O arq. VSM.INI traz a seq. de etapas, a massa do calibr. etc. F)im ou <CR>?";
DO: TCL$ = UCASE$(INKEY$): LOOP WHILE TCL$ = ""
IF TCL$ = "F" THEN END
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "Lendo arquivo VSM.INI...";
OPEN "VSM.INI" FOR INPUT AS #1
WHILE NOT EOF(1)
 INPUT #1, CHAV$: LINE INPUT #1, RESTO$
 SELECT CASE CHAV$
 CASE "SEQ.ETAPAS"
  NETAP = 0
  WHILE RESTO$ <> ""
   NETAP = NETAP + 1: ETAPA(NETAP) = VAL(RESTO$)
   RESTO$ = MID$(RESTO$, LEN(STR$(ETAPA(NETAP))) + 1)
  WEND
 CASE "MASS.CALIBRADOR"
  MASSCAL = VAL(RESTO$)
 CASE "ARQ.PORTA-AMOSTRAS"
  ARQPA$ = RESTO$
 END SELECT
WEND: CLOSE

LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Inic. 1/4] Colocar 'VSM/OFF/DIAL' em 'VSM' (=controle via computador) e <CR>.";
DO: LOOP WHILE INKEY$ = ""
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Inic. 2/4] Pressionar 'SET POWER' (=eletroima desbloqueado) e <CR>.";
DO: LOOP WHILE INKEY$ = ""
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Inic. 3/4] Colocar 'ATTENUATOR' em 1 (=maxima sensibilidade) e <CR>.";
DO: LOOP WHILE INKEY$ = ""
ATENU = 1
LOCATE 9, 1: PRINT USING "Atenuacao: ####            "; ATENU
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Inic. 4/4] Inserir o calibrador (FeSO4, sal verde) no braco vibrante e <CR>.";
DO: LOOP WHILE INKEY$ = ""
LOCATE 3, 1: PRINT "Colecao: VSM.INI           ";
LOCATE 4, 1: PRINT "Amostra: CALIBRAD          ";
LOCATE 5, 1: PRINT USING "Massa [g]: ##.##           "; MASSCAL
LOCATE 7, 1: PRINT USING "Porta-amostra: #           "; PORTAMO
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "Calibrando H aplicado...";
FINO = 5000: DCAMPO = 0
CALL SETFINO(-FINO): CALL LEHALL: CAMPONEG = HALL * HALL2mT: CALL SETFINO(0)
LOCATE 11, 1: PRINT "Trecho: 3/5 (H0 -> Hmin)   ";
LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##      "; CAMPONEG; DCAMPO
CALL SETFINO(FINO): CALL LEHALL: CAMPOPOS = HALL * HALL2mT: CALL SETFINO(0)
LOCATE 11, 1: PRINT "Trecho: 1/5 (H0 -> Hmax)   ";
LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##      "; CAMPOPOS; DCAMPO
mT2FINO = 2 * FINO / (CAMPOPOS - CAMPONEG)
Y0 = 5000: Y1 = 15000
CALL SETGROSSO(Y0): CALL LEHALL: X0 = HALL * HALL2mT
LOCATE 11, 1: PRINT "Trecho: 1/5 (H0 -> Hmax)   ";
LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##      "; X0; DCAMPO
CALL SETGROSSO(Y1): CALL LEHALL: X1 = HALL * HALL2mT
LOCATE 11, 1: PRINT "Trecho: 1/5 (H0 -> Hmax)   ";
LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##      "; X1; DCAMPO
CALL SETGROSSO(Y0): CALL SETGROSSO(0)
mT2GROSSO = (Y0 * X1 * X1 - Y1 * X0 * X0) / (X0 * X1 * X1 - X0 * X0 * X1)
mT2GROSSO2 = (Y1 * X0 - Y0 * X1) / (X0 * X1 * X1 - X0 * X0 * X1)

LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "Calibrando H aplicado [OK]. Calibrando M medido...";
GROSSO = 5000
CALL SETGROSSO(-GROSSO)
LOCATE 11, 1: PRINT "Trecho: 3/5 (H0 -> Hmin)   ";
LOCATE 12, 1: PRINT "H [mT]:                    ";
LOCATE 13, 1: PRINT "M [uAm2]:                  ";
CALL LEHALL: CAMPO1 = HALL * HALL2mT
CALL LESINAL: MAG = AMPL * ATENU: DMAG = DAMPL * ATENU: FASENEG = FASE
CALL PLOTSINAL
CALL LEHALL: CAMPO2 = HALL * HALL2mT
CALL SETGROSSO(0)
CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG * SINAL2uAm2; DMAG * SINAL2uAm2
CAMPONEG = CAMPO: MAGNEG = MAG
CALL SETGROSSO(GROSSO)
LOCATE 11, 1: PRINT "Trecho: 1/5 (H0 -> Hmax)   ";
LOCATE 12, 1: PRINT "H [mT]:                    ";
LOCATE 13, 1: PRINT "M [uAm2]:                  ";
CALL LEHALL: CAMPO1 = HALL * HALL2mT
CALL LESINAL: MAG = AMPL * ATENU: DMAG = DAMPL * ATENU: FASEPOS = FASE
CALL PLOTSINAL
CALL LEHALL: CAMPO2 = HALL * HALL2mT
CALL SETGROSSO(0)
CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG * SINAL2uAm2; DMAG * SINAL2uAm2
CAMPOPOS = CAMPO: MAGPOS = MAG
MAG = (MAGPOS - MAGNEG) / 2: CAMPO = (CAMPOPOS - CAMPONEG) / 2
SINAL2uAm2 = 1000 * MASSCAL * SUSCAL * CAMPO / (4 * PI * .0000001) / MAG
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "Calibrando H aplicado [OK]. Calibrando M medido [OK]. Retirar calibrador e <CR>.";
DO: LOOP WHILE INKEY$ = ""

CONFIG:
LOCATE 3, 1: PRINT "Colecao:                   ";
LOCATE 4, 1: PRINT "Amostra:                   ";
LOCATE 5, 1: PRINT "Massa [g]:                 ";
LOCATE 7, 1: PRINT "Porta-amostra:             ";
LOCATE 9, 1: PRINT USING "Atenuacao: ####            "; ATENU
LOCATE 11, 1: PRINT "Trecho:                    ";
LOCATE 12, 1: PRINT "H [mT]:                    ";
LOCATE 13, 1: PRINT "M [uAm2]:                  ";
DO
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: INPUT ; "[Config. 1/5] Nome da colecao (arquivo dos dados)"; COLE$
LOOP WHILE COLE$ = ""
COLE$ = UCASE$(COLE$)
IF INSTR(COLE$, ".") = 0 THEN COLE$ = LEFT$(COLE$, 8) + ".VSM"
LOCATE 3, 1: PRINT USING "Colecao: \          \      "; COLE$
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Config. 2/5] Executar ciclo de: R)emanescencia, H)isterese ou A)mbos? [A]";
DO: TCL$ = UCASE$(INKEY$): LOOP WHILE TCL$ <> "R" AND TCL$ <> "H" AND TCL$ <> "A" AND TCL$ <> CHR$(13)
IF TCL$ = CHR$(13) THEN TCL$ = "A"
QUAL$ = TCL$
IF QUAL$ = "A" THEN
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "[Config. 3/5] Executar ciclos: em P)aralelo ou em S)erie? [P]";
 DO: TCL$ = UCASE$(INKEY$): LOOP WHILE TCL$ <> "P" AND TCL$ <> "S" AND TCL$ <> CHR$(13)
 IF TCL$ = CHR$(13) THEN TCL$ = "P"
 COMO$ = TCL$
END IF
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Config. 4/5] Iniciar o ciclo em: H= Z)ero ou H= M)aximo? [Z]";
DO: TCL$ = UCASE$(INKEY$): LOOP WHILE TCL$ <> "Z" AND TCL$ <> "M" AND TCL$ <> CHR$(13)
IF TCL$ = CHR$(13) THEN TCL$ = "Z"
ONDESAI$ = TCL$
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Config. 5/5] Reverter/terminar o ciclo em: H= M)inimo/maximo ou M= Z)ero? [M]";
DO: TCL$ = UCASE$(INKEY$): LOOP WHILE TCL$ <> "Z" AND TCL$ <> "M" AND TCL$ <> CHR$(13)
IF TCL$ = CHR$(13) THEN TCL$ = "M"
ONDEVIRA$ = TCL$

MEDID:
LOCATE 3, 1: PRINT USING "Colecao: \          \      "; COLE$
LOCATE 4, 1: PRINT "Amostra:                   ";
LOCATE 5, 1: PRINT "Massa [g]:                 ";
LOCATE 7, 1: PRINT "Porta-amostra:             ";
LOCATE 9, 1: PRINT USING "Atenuacao: ####            "; ATENU
LOCATE 11, 1: PRINT "Trecho:                    ";
LOCATE 12, 1: PRINT "H [mT]:                    ";
LOCATE 13, 1: PRINT "M [uAm2]:                  ";
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: INPUT ; "[Medid. 1/4] Nome da amostra"; AMO$
AMO$ = UCASE$(AMO$)
LOCATE 4, 1: PRINT USING "Amostra: \                \"; AMO$
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Medid. 2/4] Numero (1-9) do porta-amostra (0 p/ nao descontar)? [0]";
DO: TCL$ = UCASE$(INKEY$): LOOP WHILE TCL$ = ""
PORTAMO = VAL(TCL$)
LOCATE 7, 1: PRINT USING "Porta-amostra: #           "; PORTAMO
IF PORTAMO = 0 THEN
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: INPUT ; "[Medid. 3/4] Massa da amostra [g]"; MASSAMO
ELSE
 OPEN ARQPA$ FOR INPUT AS #1
 MASSPA = 0: NHH = 0: NHHR = 0
 WHILE NOT EOF(1)
  INPUT #1, CHAV$
  SELECT CASE UCASE$(CHAV$)
  CASE "AMO"
   INPUT #1, NOME$, MASSA, DAT$
   IF INSTR(NOME$, TXT$(PORTAMO)) <> 0 THEN MASSPA = MASSA
  CASE "HIS"
   INPUT #1, HH, DHH, MM, DMM
   IF INSTR(NOME$, TXT$(PORTAMO)) THEN NHH = NHH + 1: MPA(NHH) = MM
  CASE "REM"
   INPUT #1, HHR, DHHR, MMR, DMMR
   IF INSTR(NOME$, TXT$(PORTAMO)) THEN NHHR = NHHR + 1: MRPA(NHHR) = MMR
  CASE ""
  CASE ELSE
   LINE INPUT #1, RESTO$
  END SELECT
 WEND: CLOSE
 NETAPH = (NHH + 3) / 3: NETAPR = (NHHR + 1) / 2
 IF MASSPA <> 0 AND NETAPH = NETAP AND NETAPR = NETAP THEN
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: INPUT ; "[Medid. 3/4] Massa [g]: amostra + porta-amost.+ tampa + PVC(6cm*6cm)"; MASSTOT
  MASSAMO = MASSTOT - MASSPA
 ELSE
  PORTAMO = 0
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: INPUT ; "[Medid. 3/4] N.o de etapas/nome do porta-amostra ruim. Massa da amostra"; MASSAMO
 END IF
END IF
LOCATE 5, 1: PRINT USING "Massa [g]: ##.##           "; MASSAMO
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "[Medid. 4/4] Inserir amostra e <CR>. Opcao: A)bortar (agora ou durante o ciclo).";
ABORTOU = 0
DO: TCL$ = UCASE$(INKEY$): LOOP WHILE TCL$ = ""
IF TCL$ = "A" THEN VEZ = 2: ABORTOU = 1: GOTO FINALIZ
VEZ = 1: HMIN = 0: HMAX = 0: MMIN = 0: MMAX = 0
FOR TRECHO = 1 TO 5: M(TRECHO, 0) = 0: MR(TRECHO, 0) = 0: NEXT
C0 = 1: CF = 27: X0 = 8 * C0 - 4: XF = 8 * CF - 4: XC = (XF + X0) / 2
L0 = 15: LF = 29: Y0 = 16 * L0 - 8: YF = 16 * LF - 8: YC = (YF + Y0) / 2
VIEW (X0, Y0)-(XF, YF), , 15: CLS
LINE (0, YC - Y0)-(XF - X0, YC - Y0)
LOCATE L0, C0 + 1: PRINT "Sinal da Magnetizacao (M)"
C0 = 28: CF = 80: X0 = 8 * C0 - 4: XF = 8 * CF - 4: XC = (XF + X0) / 2
L0 = 2: LF = 29: Y0 = 16 * L0 - 8: YF = 16 * LF - 8: YC = (YF + Y0) / 2
VIEW (X0, Y0)-(XF, YF), , 15: CLS
LINE (0, YC - Y0)-(XF - X0, YC - Y0): LINE (XC - X0, 0)-(XC - X0, YF - Y0)
LOCATE L0, C0 + 8: PRINT "Campo Aplicado (H) x Magnetizacao (M)"

TRECHO1:
IF ONDESAI$ = "Z" THEN
 TRECHO = 1
 LOCATE 11, 1: PRINT "Trecho: 1/5 (H0 -> Hmax)   ";
 FOR ET = 1 TO NETAP - 1
  LOCATE 12, 1: PRINT "H [mT]:                    ";
  LOCATE 13, 1: PRINT "M [uAm2]:                  ";
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Aplicando campo H (aprox."; ETAPA(ET); "mT)...";
  IF ETAPA(ET) = 0 THEN
   CALL SETZERO
  ELSE
   GROSSO = INT(ETAPA(ET) * mT2GROSSO + ETAPA(ET) * ETAPA(ET) * mT2GROSSO2)
   CALL SETFINO(0): CALL SETGROSSO(GROSSO)
  END IF
  CALL LEHALL: CAMPO1 = HALL * HALL2mT
  IF QUAL$ = "H" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 2) THEN
   M(TRECHO, 0) = M(TRECHO, 0) + 1
   LOCATE 30, 1: PRINT SPACE$(80);
   LOCATE 30, 1: PRINT "Medindo magnetizacao M (histerese)...";
   CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
   IF PORTAMO <> 0 THEN MAG = MAG - MPA(ET)
   LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
   CALL PLOTSINAL
   M(TRECHO, ET) = MAG: DM(TRECHO, ET) = DMAG
   IF MMAX < M(TRECHO, ET) THEN MMAX = M(TRECHO, ET) ELSE IF MMIN > M(TRECHO, ET) THEN MMIN = M(TRECHO, ET)
  END IF
  CALL LEHALL: CAMPO2 = HALL * HALL2mT
  CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
  LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
  H(TRECHO, ET) = CAMPO: DH(TRECHO, ET) = DCAMPO
  IF HMAX < H(TRECHO, ET) THEN HMAX = H(TRECHO, ET) ELSE IF HMIN > H(TRECHO, ET) THEN HMIN = H(TRECHO, ET)
  IF QUAL$ = "R" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 1) THEN
   HR(TRECHO, ET) = CAMPO: DHR(TRECHO, ET) = DCAMPO
   MR(TRECHO, 0) = MR(TRECHO, 0) + 1
   LOCATE 30, 1: PRINT SPACE$(80);
   LOCATE 30, 1: PRINT "Zerando campo H...";
   CALL SETZERO
   LOCATE 30, 1: PRINT SPACE$(80);
   LOCATE 30, 1: PRINT "Medindo magnetizacao M (remanescencia)...";
   CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
   IF PORTAMO <> 0 THEN MAG = MAG - MRPA(ET)
   LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
   CALL PLOTSINAL
   MR(TRECHO, ET) = MAG: DMR(TRECHO, ET) = DMAG
   IF MMAX < MR(TRECHO, ET) THEN MMAX = MR(TRECHO, ET) ELSE IF MMIN > MR(TRECHO, ET) THEN MMIN = MR(TRECHO, ET)
  END IF
  CALL PLOTHM
  TCL$ = UCASE$(INKEY$): IF TCL$ = "A" THEN VEZ = 2: ABORTOU = 1: GOTO FINALIZ
 NEXT
END IF

TRECHO2:
TRECHO = 2
ET = 1: ET2 = NETAP
LOCATE 11, 1: PRINT "Trecho: 2/5 (Hmax -> H0)   ";
LOCATE 12, 1: PRINT "H [mT]:                    ";
LOCATE 13, 1: PRINT "M [uAm2]:                  ";
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "Aplicando campo H (aprox."; ETAPA(ET2); "mT)...";
IF ETAPA(ET2) = 0 THEN
 CALL SETZERO
ELSE
 GROSSO = INT(ETAPA(ET2) * mT2GROSSO + ETAPA(ET2) * ETAPA(ET2) * mT2GROSSO2)
 CALL SETFINO(0): CALL SETGROSSO(GROSSO)
END IF
CALL LEHALL: CAMPO1 = HALL * HALL2mT
IF QUAL$ = "H" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 2) THEN
 M(TRECHO, 0) = M(TRECHO, 0) + 1
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Medindo magnetizacao M (histerese)...";
 CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
 IF PORTAMO <> 0 THEN MAG = MAG - MPA(NETAP)
 LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
 CALL PLOTSINAL
 M(TRECHO, ET) = MAG: DM(TRECHO, ET) = DMAG
 IF MMAX < M(TRECHO, ET) THEN MMAX = M(TRECHO, ET) ELSE IF MMIN > M(TRECHO, ET) THEN MMIN = M(TRECHO, ET)
END IF
CALL LEHALL: CAMPO2 = HALL * HALL2mT
CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
H(TRECHO, ET) = CAMPO: DH(TRECHO, ET) = DCAMPO
IF HMAX < H(TRECHO, ET) THEN HMAX = H(TRECHO, ET) ELSE IF HMIN > H(TRECHO, ET) THEN HMIN = H(TRECHO, ET)
IF QUAL$ = "R" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 1) THEN
 HR(TRECHO, ET) = CAMPO: DHR(TRECHO, ET) = DCAMPO
 MR(TRECHO, 0) = MR(TRECHO, 0) + 1
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Zerando campo H...";
 CALL SETZERO
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Medindo magnetizacao M (remanescencia)...";
 CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
 IF PORTAMO <> 0 THEN MAG = MAG - MRPA(NETAP)
 LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
 CALL PLOTSINAL
 MR(TRECHO, ET) = MAG: DMR(TRECHO, ET) = DMAG
 IF MMAX < MR(TRECHO, ET) THEN MMAX = MR(TRECHO, ET) ELSE IF MMIN > MR(TRECHO, ET) THEN MMIN = MR(TRECHO, ET)
END IF
CALL PLOTHM
TCL$ = UCASE$(INKEY$): IF TCL$ = "A" THEN VEZ = 2: ABORTOU = 1: GOTO FINALIZ
IF QUAL$ = "H" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 2) THEN
 FOR ET = 2 TO NETAP - 1
  ET2 = NETAP - ET + 1
  LOCATE 12, 1: PRINT "H [mT]:                    ";
  LOCATE 13, 1: PRINT "M [uAm2]:                  ";
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Aplicando campo H (aprox."; ETAPA(ET2); "mT)...";
  IF ETAPA(ET2) = 0 THEN
   CALL SETZERO
  ELSE
   GROSSO = INT(ETAPA(ET2) * mT2GROSSO + ETAPA(ET2) * ETAPA(ET2) * mT2GROSSO2)
   CALL SETFINO(0): CALL SETGROSSO(GROSSO)
  END IF
  CALL LEHALL: CAMPO1 = HALL * HALL2mT
  M(TRECHO, 0) = M(TRECHO, 0) + 1
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Medindo magnetizacao M (histerese)...";
  CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
  IF PORTAMO <> 0 THEN MAG = MAG - MPA(NETAP + ET - 1)
  LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
  CALL PLOTSINAL
  M(TRECHO, ET) = MAG: DM(TRECHO, ET) = DMAG
  IF MMAX < M(TRECHO, ET) THEN MMAX = M(TRECHO, ET) ELSE IF MMIN > M(TRECHO, ET) THEN MMIN = M(TRECHO, ET)
  CALL LEHALL: CAMPO2 = HALL * HALL2mT
  CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
  LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
  H(TRECHO, ET) = CAMPO: DH(TRECHO, ET) = DCAMPO
  IF HMAX < H(TRECHO, ET) THEN HMAX = H(TRECHO, ET) ELSE IF HMIN > H(TRECHO, ET) THEN HMIN = H(TRECHO, ET)
  CALL PLOTHM
  TCL$ = UCASE$(INKEY$): IF TCL$ = "A" THEN VEZ = 2: ABORTOU = 1: GOTO FINALIZ
 NEXT
END IF

TRECHO3:
TRECHO = 3
LOCATE 11, 1: PRINT "Trecho: 3/5 (H0 -> Hmin)   ";
FOR ET = 1 TO NETAP - 1
 LOCATE 12, 1: PRINT "H [mT]:                    ";
 LOCATE 13, 1: PRINT "M [uAm2]:                  ";
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Aplicando campo H (aprox."; -ETAPA(ET); "mT)...";
 IF ETAPA(ET) = 0 THEN
  CALL SETZERO
 ELSE
  GROSSO = INT(ETAPA(ET) * mT2GROSSO + ETAPA(ET) * ETAPA(ET) * mT2GROSSO2)
  CALL SETFINO(0): CALL SETGROSSO(-GROSSO)
 END IF
 CALL LEHALL: CAMPO1 = HALL * HALL2mT
 IF QUAL$ = "H" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 2) THEN
  M(TRECHO, 0) = M(TRECHO, 0) + 1
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Medindo magnetizacao M (histerese)...";
  CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
  IF PORTAMO <> 0 THEN MAG = MAG - MPA(2 * NETAP + ET - 2)
  LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
  CALL PLOTSINAL
  M(TRECHO, ET) = MAG: DM(TRECHO, ET) = DMAG
  IF MMAX < M(TRECHO, ET) THEN MMAX = M(TRECHO, ET) ELSE IF MMIN > M(TRECHO, ET) THEN MMIN = M(TRECHO, ET)
 END IF
 CALL LEHALL: CAMPO2 = HALL * HALL2mT
 CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
 LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
 H(TRECHO, ET) = CAMPO: DH(TRECHO, ET) = DCAMPO
 IF HMAX < H(TRECHO, ET) THEN HMAX = H(TRECHO, ET) ELSE IF HMIN > H(TRECHO, ET) THEN HMIN = H(TRECHO, ET)
 IF QUAL$ = "R" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 1) THEN
  HR(TRECHO, ET) = CAMPO: DHR(TRECHO, ET) = DCAMPO
  MR(TRECHO, 0) = MR(TRECHO, 0) + 1
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Zerando campo H...";
  CALL SETZERO
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Medindo magnetizacao M (remanescencia)...";
  CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
  IF PORTAMO <> 0 THEN MAG = MAG - MRPA(NETAP + ET)
  LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
  CALL PLOTSINAL
  MR(TRECHO, ET) = MAG: DMR(TRECHO, ET) = DMAG
  IF MMAX < MR(TRECHO, ET) THEN MMAX = MR(TRECHO, ET) ELSE IF MMIN > MR(TRECHO, ET) THEN MMIN = MR(TRECHO, ET)
 END IF
 CALL PLOTHM
 TCL$ = UCASE$(INKEY$): IF TCL$ = "A" THEN VEZ = 2: ABORTOU = 1: GOTO FINALIZ
 IF ONDEVIRA$ = "Z" THEN
  IF QUAL$ = "H" AND M(TRECHO, ET - 1) < 0 THEN GOTO TRECHO4
  IF QUAL$ = "R" AND MR(TRECHO, ET - 1) < 0 THEN GOTO TRECHO4
  IF QUAL$ = "A" THEN
   IF COMO$ = "P" AND M(TRECHO, ET - 1) < 0 AND MR(TRECHO, ET - 1) < 0 THEN GOTO TRECHO4
   IF COMO$ = "S" AND VEZ = 1 AND MR(TRECHO, ET - 1) < 0 THEN GOTO TRECHO4
   IF COMO$ = "S" AND VEZ = 2 AND M(TRECHO, ET - 1) < 0 THEN GOTO TRECHO4
  END IF
 END IF
NEXT

TRECHO4:
TRECHO = 4
ET = 1: ET2 = NETAP
LOCATE 11, 1: PRINT "Trecho: 4/5 (Hmin -> H0)   ";
LOCATE 12, 1: PRINT "H [mT]:                    ";
LOCATE 13, 1: PRINT "M [uAm2]:                  ";
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "Aplicando campo H (aprox."; -ETAPA(ET2); "mT)...";
IF ETAPA(ET2) = 0 THEN
 CALL SETZERO
ELSE
 GROSSO = INT(ETAPA(ET2) * mT2GROSSO + ETAPA(ET2) * ETAPA(ET2) * mT2GROSSO2)
 CALL SETFINO(0): CALL SETGROSSO(-GROSSO)
END IF
CALL LEHALL: CAMPO1 = HALL * HALL2mT
IF QUAL$ = "H" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 2) THEN
 M(TRECHO, 0) = M(TRECHO, 0) + 1
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Medindo magnetizacao M (histerese) e simetrizando...";
 CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
 IF PORTAMO <> 0 THEN MAG = MAG + MPA(NETAP)
 LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
 CALL PLOTSINAL
 M(TRECHO, ET) = (MAG - M(TRECHO - 2, ET)) / 2: DM(TRECHO, ET) = (DMAG + DM(TRECHO - 2, ET)) / 2
 M(TRECHO - 2, ET) = -M(TRECHO, ET): DM(TRECHO - 2, ET) = DM(TRECHO, ET)
 IF MMAX < M(TRECHO, ET) THEN MMAX = M(TRECHO, ET) ELSE IF MMIN > M(TRECHO, ET) THEN MMIN = M(TRECHO, ET)
END IF
CALL LEHALL: CAMPO2 = HALL * HALL2mT
CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
H(TRECHO, ET) = (CAMPO - H(TRECHO - 2, ET)) / 2: DH(TRECHO, ET) = (DCAMPO + DH(TRECHO - 2, ET)) / 2
H(TRECHO - 2, ET) = -H(TRECHO, ET): DH(TRECHO - 2, ET) = DH(TRECHO, ET)
IF HMAX < H(TRECHO, ET) THEN HMAX = H(TRECHO, ET) ELSE IF HMIN > H(TRECHO, ET) THEN HMIN = H(TRECHO, ET)
IF QUAL$ = "R" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 1) THEN
 HR(TRECHO, ET) = CAMPO: DHR(TRECHO, ET) = DCAMPO
 MR(TRECHO, 0) = MR(TRECHO, 0) + 1
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Zerando campo H...";
 CALL SETZERO
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Medindo magnetizacao M (remanescencia) e simetrizando...";
 CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
 IF PORTAMO <> 0 THEN MAG = MAG + MRPA(NETAP)
 LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
 CALL PLOTSINAL
 MR(TRECHO, ET) = (MAG - MR(TRECHO - 2, ET)) / 2: DMR(TRECHO, ET) = (DMAG + DMR(TRECHO - 2, ET)) / 2
 MR(TRECHO - 2, ET) = -MR(TRECHO, ET): DMR(TRECHO - 2, ET) = DMR(TRECHO, ET)
 IF MMAX < MR(TRECHO, ET) THEN MMAX = MR(TRECHO, ET) ELSE IF MMIN > MR(TRECHO, ET) THEN MMIN = MR(TRECHO, ET)
END IF
CALL PLOTHM
TCL$ = UCASE$(INKEY$): IF TCL$ = "A" THEN VEZ = 2: ABORTOU = 1: GOTO FINALIZ
IF QUAL$ = "H" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 2) THEN
 FOR ET = 2 TO NETAP - 1
  ET2 = NETAP - ET + 1
  LOCATE 12, 1: PRINT "H [mT]:                    ";
  LOCATE 13, 1: PRINT "M [uAm2]:                  ";
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Aplicando campo H (aprox."; -ETAPA(ET2); "mT)...";
  IF ETAPA(ET2) = 0 THEN
   CALL SETZERO
  ELSE
   GROSSO = INT(ETAPA(ET2) * mT2GROSSO + ETAPA(ET2) * ETAPA(ET2) * mT2GROSSO2)
   CALL SETFINO(0): CALL SETGROSSO(-GROSSO)
  END IF
  CALL LEHALL: CAMPO1 = HALL * HALL2mT
  M(TRECHO, 0) = M(TRECHO, 0) + 1
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Medindo magnetizacao M (histerese) e simetrizando...";
  CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
  IF PORTAMO <> 0 THEN MAG = MAG + MPA(NETAP + ET - 1)
  LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
  CALL PLOTSINAL
  M(TRECHO, ET) = (MAG - M(TRECHO - 2, ET)) / 2: DM(TRECHO, ET) = (DMAG + DM(TRECHO - 2, ET)) / 2
  M(TRECHO - 2, ET) = -M(TRECHO, ET): DM(TRECHO - 2, ET) = DM(TRECHO, ET)
  IF MMAX < M(TRECHO, ET) THEN MMAX = M(TRECHO, ET) ELSE IF MMIN > M(TRECHO, ET) THEN MMIN = M(TRECHO, ET)
  CALL LEHALL: CAMPO2 = HALL * HALL2mT
  CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
  LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
  H(TRECHO, ET) = (CAMPO - H(TRECHO - 2, ET)) / 2: DH(TRECHO, ET) = (DCAMPO + DH(TRECHO - 2, ET)) / 2
  H(TRECHO - 2, ET) = -H(TRECHO, ET): DH(TRECHO - 2, ET) = DH(TRECHO, ET)
  IF HMAX < H(TRECHO, ET) THEN HMAX = H(TRECHO, ET) ELSE IF HMIN > H(TRECHO, ET) THEN HMIN = H(TRECHO, ET)
  CALL PLOTHM
  TCL$ = UCASE$(INKEY$): IF TCL$ = "A" THEN VEZ = 2: ABORTOU = 1: GOTO FINALIZ
 NEXT
END IF

TRECHO5:
TRECHO = 5
LOCATE 11, 1: PRINT "Trecho: 5/5 (H0 -> Hmax)   ";
FOR ET = 1 TO NETAP - 1
 LOCATE 12, 1: PRINT "H [mT]:                    ";
 LOCATE 13, 1: PRINT "M [uAm2]:                  ";
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Aplicando campo H (aprox."; ETAPA(ET); "mT)...";
 IF ETAPA(ET) = 0 THEN
  CALL SETZERO
 ELSE
  GROSSO = INT(ETAPA(ET) * mT2GROSSO + ETAPA(ET) * ETAPA(ET) * mT2GROSSO2)
  CALL SETFINO(0): CALL SETGROSSO(GROSSO)
 END IF
 CALL LEHALL: CAMPO1 = HALL * HALL2mT
 IF QUAL$ = "H" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 2) THEN
  M(TRECHO, 0) = M(TRECHO, 0) + 1
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Medindo magnetizacao M (histerese) e simetrizando...";
  CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
  IF PORTAMO <> 0 THEN MAG = MAG + MPA(2 * NETAP + ET - 2)
  LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
  CALL PLOTSINAL
  M(TRECHO, ET) = (MAG - M(TRECHO - 2, ET)) / 2: DM(TRECHO, ET) = (DMAG + DM(TRECHO - 2, ET)) / 2
  M(TRECHO - 2, ET) = -M(TRECHO, ET): DM(TRECHO - 2, ET) = DM(TRECHO, ET)
  IF MMAX < M(TRECHO, ET) THEN MMAX = M(TRECHO, ET) ELSE IF MMIN > M(TRECHO, ET) THEN MMIN = M(TRECHO, ET)
 END IF
 CALL LEHALL: CAMPO2 = HALL * HALL2mT
 CAMPO = (CAMPO1 + CAMPO2) / 2: DCAMPO = ABS(CAMPO1 - CAMPO2) / 2
 LOCATE 12, 1: PRINT USING "H [mT]: #####.##ñ.##       "; CAMPO; DCAMPO
 H(TRECHO, ET) = (CAMPO - H(TRECHO - 2, ET)) / 2: DH(TRECHO, ET) = (DCAMPO + DH(TRECHO - 2, ET)) / 2
 H(TRECHO - 2, ET) = -H(TRECHO, ET): DH(TRECHO - 2, ET) = DH(TRECHO, ET)
 IF HMAX < H(TRECHO, ET) THEN HMAX = H(TRECHO, ET) ELSE IF HMIN > H(TRECHO, ET) THEN HMIN = H(TRECHO, ET)
 IF QUAL$ = "R" OR (QUAL$ = "A" AND COMO$ = "P") OR (QUAL$ = "A" AND COMO$ = "S" AND VEZ = 1) THEN
  HR(TRECHO, ET) = CAMPO: DHR(TRECHO, ET) = DCAMPO
  MR(TRECHO, 0) = MR(TRECHO, 0) + 1
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Zerando campo H...";
  CALL SETZERO
  LOCATE 30, 1: PRINT SPACE$(80);
  LOCATE 30, 1: PRINT "Medindo magnetizacao M (remanescencia) e simetrizando...";
  CALL LESINAL: MAG = AMPL * ATENU * SINAL2uAm2: DMAG = DAMPL * ATENU * SINAL2uAm2
  IF PORTAMO <> 0 THEN MAG = MAG + MRPA(NETAP + ET)
  LOCATE 13, 1: PRINT USING "M [uAm2]: ######.##ñ#.##   "; MAG; DMAG
  CALL PLOTSINAL
  MR(TRECHO, ET) = (MAG - MR(TRECHO - 2, ET)) / 2: DMR(TRECHO, ET) = (DMAG + DMR(TRECHO - 2, ET)) / 2
  MR(TRECHO - 2, ET) = -MR(TRECHO, ET): DMR(TRECHO - 2, ET) = DMR(TRECHO, ET)
  IF MMAX < MR(TRECHO, ET) THEN MMAX = MR(TRECHO, ET) ELSE IF MMIN > MR(TRECHO, ET) THEN MMIN = MR(TRECHO, ET)
 END IF
 CALL PLOTHM
 TCL$ = UCASE$(INKEY$): IF TCL$ = "A" THEN VEZ = 2: ABORTOU = 1: GOTO FINALIZ
 IF ONDEVIRA$ = "Z" THEN
  IF QUAL$ = "H" AND M(TRECHO, ET - 1) > 0 THEN GOTO FINALIZ
  IF QUAL$ = "R" AND MR(TRECHO, ET - 1) > 0 THEN GOTO FINALIZ
  IF QUAL$ = "A" THEN
   IF COMO$ = "P" AND M(TRECHO, ET - 1) > 0 AND MR(TRECHO, ET - 1) > 0 THEN GOTO FINALIZ
   IF COMO$ = "S" AND VEZ = 1 AND MR(TRECHO, ET - 1) > 0 THEN GOTO FINALIZ
   IF COMO$ = "S" AND VEZ = 2 AND M(TRECHO, ET - 1) > 0 THEN GOTO FINALIZ
  END IF
 END IF
NEXT

FINALIZ:
CALL SETFINO(0): CALL SETGROSSO(0)
IF VEZ = 1 AND COMO$ = "S" THEN VEZ = 2: GOTO TRECHO1

'gravar
IF ABORTOU = 0 THEN
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Gravando amostra: "; AMO$; " no arquivo: "; COLE$;
 OPEN COLE$ FOR APPEND AS #1
 DAY$ = DATE$: DIA$ = MID$(DAY$, 4, 2) + "/" + LEFT$(DAY$, 2) + "/" + RIGHT$(DAY$, 4)
 WRITE #1, "AMO", AMO$, APROX(MASSAMO, 2), DIA$
 FOR I = 1 TO 3: FOR J = 1 TO MR(I, 0)
  WRITE #1, "REM", APROX(HR(I, J), 2), APROX(DHR(I, J), 2), APROX(MR(I, J), 2), APROX(DMR(I, J), 2)
 NEXT: NEXT
 FOR I = 1 TO 3: FOR J = 1 TO M(I, 0)
  WRITE #1, "HIS", APROX(H(I, J), 2), APROX(DH(I, J), 2), APROX(M(I, J), 2), APROX(DM(I, J), 2)
 NEXT: NEXT
 CLOSE
END IF
BEEP: BEEP: BEEP
IF ATENU <> 1 THEN
 ATENU = 1
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Colocar 'ATTENUATOR' em 1 e <CR>.";
 DO: LOOP WHILE INKEY$ = ""
END IF
LOCATE 30, 1: PRINT SPACE$(80);
LOCATE 30, 1: PRINT "P)rox. amostra, mudar C)onfiguracao (arq./estrategia), R)einicializar ou F)im?";
DO: TCL$ = UCASE$(INKEY$): LOOP WHILE TCL$ <> "P" AND TCL$ <> "C" AND TCL$ <> "R" AND TCL$ <> "F"
IF TCL$ = "P" THEN GOTO MEDID
IF TCL$ = "C" THEN GOTO CONFIG
IF TCL$ = "R" THEN GOTO INIC
IF TCL$ = "F" THEN SCREEN 0: END
END

FUNCTION APROX (X, N)
  'Deixa X com N casas decimais.
IF X < 0 THEN X1$ = "-000000000" + LTRIM$(STR$(INT(-X * 10 ^ N + .5)))
IF X >= 0 THEN X1$ = "000000000" + LTRIM$(STR$(INT(X * 10 ^ N + .5)))
X2$ = LEFT$(X1$, LEN(X1$) - N) + "." + RIGHT$(X1$, N)
APROX = VAL(X2$)
END FUNCTION

SUB DIMTOXYZ (DEC, INC, M, X, Y, Z)
  'Dados: declinacao, inclinacao (em graus) e modulo, calcula as coordenadas
  'X, Y e Z do vetor correspondente.
X = M * COS(DEC * GR) * COS(INC * GR)
Y = M * SIN(DEC * GR) * COS(INC * GR)
Z = M * SIN(INC * GR)
END SUB

SUB GETHALL
HZ = 60: FREQ = CINT(1193280 / HZ)
CLI& = &HCAFA&: DEF SEG = VARSEG(CLI&): CALL ABSOLUTE(VARPTR(CLI&)) 'desabilita interrupcoes
OUT PROGPIT, &HB6 '10=counter2, 11=lo,hi, 011=mode3, 0=binary
OUT COUNT2, FREQ AND &HFF: OUT COUNT2, FREQ \ 256
OUT PPI, INP(PPI) OR &H1 'liga o som
OUT A2, 2: OUT A2, 3 'conversao AD 16 bits
FOR I = 1 TO 12
 WAIT PPI, &H20: WAIT PPI, &H20, &H20 'espera o som
NEXT
OUT A2, 9: ERRHALL = INP(A1) AND 128 'codigo de erro
OUT A2, 1: HALL = INP(A1) 'byte alto
OUT A2, 3: HALL = HALL * 256 + INP(A1) 'byte baixo
OUT A2, 2
IF (HALL AND 32768) = 0 THEN HALL = -HALL ELSE HALL = (HALL AND 32767)
'LOCATE 12, 1: PRINT USING "H [mT]: +####.##          "; HALL * HALL2mT;
OUT PPI, INP(PPI) AND &HFC 'desliga o som
STI& = &HCAFB&: DEF SEG = VARSEG(STI&): CALL ABSOLUTE(VARPTR(STI&)) 'habilita interrupcoes
END SUB

SUB LEHALL
HALLANT = 999: HALL = 0
WHILE ABS(HALLANT - HALL) > 1 OR ERRHALL <> 0
 HALLANT = HALL
 CALL GETHALL
 LOCATE 12, 1: PRINT USING "H [mT]: +####.##ñ###.##   "; HALL * HALL2mT; ABS(HALL - HALLANT) * HALL2mT;
 COMEC = TIMER: DO: LOOP WHILE TIMER - COMEC < .3
WEND
END SUB

SUB LESINAL
CONST BORD = 8
DIM ALTO(-BORD TO NVIB + BORD, FATIAS), BAIXO(-BORD TO NVIB + BORD, FATIAS)
DIM PAR(NVIB), IMPAR(NVIB)
LEIT:
HZ = FATIAS * OTIMA: FREQ = CINT(1193280 / HZ)
CLI& = &HCAFA&: DEF SEG = VARSEG(CLI&): CALL ABSOLUTE(VARPTR(CLI&)) 'desabilita interrupcoes
OUT PROGPIT, &HB6 '10=counter2, 11=lo,hi, 011=mode3, 0=binary
OUT COUNT2, FREQ AND &HFF: OUT COUNT2, FREQ \ 256
OUT PPI, INP(PPI) OR &H1 'liga o som
FOR I = -BORD TO NVIB + BORD: FOR J = 1 TO FATIAS
 OUT C1, VIBR(J) 'move a amostra
 OUT B2, 2: OUT B2, 0 'conversao AD 12 bits
 WAIT PPI, &H20: WAIT PPI, &H20, &H20 'espera o som
 OUT B2, 2: ALTO(I, J) = INP(B1) '8 bits altos
 OUT B2, 3: BAIXO(I, J) = INP(B1) '4 bits baixos
NEXT: NEXT
OUT PPI, INP(PPI) AND &HFC 'desliga o som
STI& = &HCAFB&: DEF SEG = VARSEG(STI&): CALL ABSOLUTE(VARPTR(STI&)) 'habilita interrupcoes
ZERO = 0: MEDPAR = 0: MEDIMPAR = 0
FOR I = 1 TO NVIB
 PAR(I) = 0: IMPAR(I) = 0
 FOR J = 1 TO FATIAS
  SINAL(I, J) = ALTO(I, J) * 16 + BAIXO(I, J) \ 16 - 2048
  ZERO = ZERO + SINAL(I, J)
  PAR(I) = PAR(I) + COSSENO(J) * SINAL(I, J) + COSSENO(J) * SINAL(I, J)
  IMPAR(I) = IMPAR(I) + SENO(J) * SINAL(I, J) + SENO(J) * SINAL(I, J)
 NEXT
 PAR(I) = PAR(I) / FATIAS: IMPAR(I) = IMPAR(I) / FATIAS
 MEDPAR = MEDPAR + PAR(I): MEDIMPAR = MEDIMPAR + IMPAR(I)
NEXT
ZERO = ZERO / FATIAS / NVIB
MEDPAR = MEDPAR / NVIB: MEDIMPAR = MEDIMPAR / NVIB
'XYZTODIM MEDIMPAR, 0, -MEDPAR, LIXO, FASE, AMPL
'LOCATE 1, 1: PRINT FASE
XYZTODIM MEDIMPAR, MEDPAR, 0, FASE, LIXO, AMPL
IF AMPL > 2000 AND ATENU < 1000 THEN
 ATENU = ATENU * 10
 LOCATE 30, 1: PRINT SPACE$(80);
 LOCATE 30, 1: PRINT "Colocar 'ATTENUATOR' em "; ATENU; "e <CR>.";
 DO: BEEP: LOOP WHILE INKEY$ = ""
 LOCATE 9, 1: PRINT USING "Atenuacao: ####            "; ATENU
 GOTO LEIT
END IF
VAMPL = 0: VFASE = 0
FOR I = 1 TO NVIB
 'XYZTODIM IMPAR(I), 0, -PAR(I), LIXO, FASEI, AMPLI
 XYZTODIM IMPAR(I), PAR(I), 0, FASEI, LIXO, AMPLI
 VAMPL = VAMPL + (AMPL - AMPLI) * (AMPL - AMPLI)
 VFASE = VFASE + (FASE - FASEI) * (FASE - FASEI)
NEXT
VAMPL = VAMPL / NVIB: DAMPL = SQR(VAMPL) / FATIAS
VFASE = VFASE / NVIB: DFASE = SQR(VFASE) / FATIAS
'AMPL = SGN(FASE) * AMPL
AMPLTOT = AMPL
DIMTOXYZ FASE, 0, AMPL, XA1, YA1, ZA1
DIMTOXYZ FASEPOS, 0, 1, XA2, YA2, ZA2
AMPL = XA1 * XA2 + YA1 * YA2 + ZA1 * ZA2
END SUB

SUB PLOTHM
IF HMAX = 0 THEN HMAX = 1
IF MMAX = 0 THEN MMAX = .1
IF HMIN = 0 THEN HMIN = -1
IF MMIN = 0 THEN MMIN = -.1
IF -HMIN < HMAX THEN HMIN = -HMAX ELSE HMAX = -HMIN
IF -MMIN < MMAX THEN MMIN = -MMAX ELSE MMAX = -MMIN
C0 = 28: CF = 80: X0 = 8 * C0 - 4: XF = 8 * CF - 4: XC = (XF + X0) / 2
L0 = 2: LF = 29: Y0 = 16 * L0 - 8: YF = 16 * LF - 8: YC = (YF + Y0) / 2
VIEW (X0, Y0)-(XF, YF), , 15: CLS
'LINE (0, YC - Y0)-(XF - X0, YC - Y0): LINE (XC - X0, 0)-(XC - X0, YF - Y0)
LOCATE L0, C0 + 8: PRINT "Campo Aplicado (H) x Magnetizacao (M)"
LOCATE L0 + 1, (C0 + CF) / 2: PRINT APROX(MMAX, 2);
LOCATE (L0 + LF) / 2 + 1, CF - 4: PRINT USING "####"; HMAX;
WINDOW (1.1 * HMIN, 1.1 * MMIN)-(1.1 * HMAX, 1.1 * MMAX)
LINE (0, 1.1 * MMIN)-(0, 1.1 * MMAX): LINE (1.1 * HMIN, 0)-(1.1 * HMAX, 0)
MJA = 0
FOR I = 1 TO 5: FOR J = 1 TO M(I, 0)
 IF MJA = 1 THEN LINE -(H(I, J), M(I, J)), 7
 MJA = 1
 LINE (H(I, J) - DH(I, J), M(I, J) - DM(I, J))-(H(I, J) + DH(I, J), M(I, J) + DM(I, J)), , B
 PSET (H(I, J), M(I, J))
NEXT: NEXT
MRJA = 0
FOR I = 1 TO 5: FOR J = 1 TO MR(I, 0)
 IF MRJA = 1 THEN LINE -(HR(I, J), MR(I, J)), 8
 MRJA = 1
 LINE (HR(I, J) - DHR(I, J), MR(I, J) - DMR(I, J))-(HR(I, J) + DHR(I, J), MR(I, J) + DMR(I, J)), , B
 PSET (HR(I, J), MR(I, J))
NEXT: NEXT
VIEW: WINDOW
END SUB

SUB PLOTSINAL
C0 = 1: CF = 27: X0 = 8 * C0 - 4: XF = 8 * CF - 4: XC = (XF + X0) / 2
L0 = 15: LF = 29: Y0 = 16 * L0 - 8: YF = 16 * LF - 8: YC = (YF + Y0) / 2
VIEW (X0, Y0)-(XF, YF), , 15: CLS
LOCATE L0, C0 + 1: PRINT "Sinal da Magnetizacao (M)"
WINDOW (1, -2 * (AMPLTOT + 2 * DAMPL * FATIAS) + ZERO)-(FATIAS, 2 * (AMPLTOT + 2 * DAMPL * FATIAS) + ZERO)
LINE (1, ZERO - 1)-(FATIAS, ZERO + 1), , B
LINE (1, -2048)-(FATIAS, 2047), , B
FOR I = 1 TO NVIB
 PSET (1, SINAL(I, 1))
 FOR J = 1 TO FATIAS: LINE -(J, SINAL(I, J)), 8: NEXT
NEXT
LOCATE L0, C0 + 1: PRINT "Sinal da Magnetizacao (M)"
VIEW: WINDOW
END SUB

SUB SETFINO (FINO) : ALVO = FINO
IF ALVO > 32700 THEN ALVO = 32700
IF ALVO < -32700 THEN ALVO = -32700
IF ALVO < 0 THEN ALVO = ALVO + 65536
HZ = 40: FREQ = CINT(1193280 / HZ)
CLI& = &HCAFA&: DEF SEG = VARSEG(CLI&): CALL ABSOLUTE(VARPTR(CLI&)) 'desabilita interrupcoes
OUT PROGPIT, &HB6 '10=counter2, 11=lo,hi, 011=mode3, 0=binary
OUT COUNT2, FREQ AND &HFF: OUT COUNT2, FREQ \ 256
OUT PPI, INP(PPI) OR &H1 'liga o som
MASCARA = 32768
FOR I = 15 TO 0 STEP -1
 IF (ABS(ALVO) AND MASCARA) = 0 THEN
  OUT C2, 1: WAIT PPI, &H20: WAIT PPI, &H20, &H20
  OUT C2, 9: WAIT PPI, &H20: WAIT PPI, &H20, &H20
  OUT C2, 1
 END IF
 IF (ABS(ALVO) AND MASCARA) <> 0 THEN
  OUT C2, 5: WAIT PPI, &H20: WAIT PPI, &H20, &H20
  OUT C2, 13: WAIT PPI, &H20: WAIT PPI, &H20, &H20
  OUT C2, 5
 END IF
MASCARA = MASCARA \ 2
NEXT I
OUT B2, 19: WAIT PPI, &H20: WAIT PPI, &H20, &H20
OUT C2, 1: WAIT PPI, &H20: WAIT PPI, &H20, &H20
OUT C2, 9: WAIT PPI, &H20: WAIT PPI, &H20, &H20
OUT C2, 1
OUT B2, 3
OUT PPI, INP(PPI) AND &HFC 'desliga o som
STI& = &HCAFB&: DEF SEG = VARSEG(STI&): CALL ABSOLUTE(VARPTR(STI&)) 'habilita interrupcoes
END SUB

SUB SETGROSSO (GROSSO) : ALVO = GROSSO
IF ALVO > 21000 THEN ALVO = 21000
IF ALVO < -21000 THEN ALVO = -21000
HZ = 40: FREQ = CINT(1193280 / HZ)
CLI& = &HCAFA&: DEF SEG = VARSEG(CLI&): CALL ABSOLUTE(VARPTR(CLI&)) 'desabilita interrupcoes
OUT PROGPIT, &HB6 '10=counter2, 11=lo,hi, 011=mode3, 0=binary
OUT COUNT2, FREQ AND &HFF: OUT COUNT2, FREQ \ 256
OUT PPI, INP(PPI) OR &H1 'liga o som
IF ALVO > 0 THEN
 OUT C2, 33: WAIT PPI, &H20: WAIT PPI, &H20, &H20 'espera o som
 OUT C2, 1
END IF
IF ALVO < 0 THEN
 OUT C2, 17: WAIT PPI, &H20: WAIT PPI, &H20, &H20
 OUT C2, 1
END IF
MASCARA = 32768
FOR I = 15 TO 0 STEP -1
 IF (ABS(ALVO) AND MASCARA) = 0 THEN
  OUT C2, 1: WAIT PPI, &H20: WAIT PPI, &H20, &H20
  OUT C2, 9: WAIT PPI, &H20: WAIT PPI, &H20, &H20
  OUT C2, 1
 END IF
 IF (ABS(ALVO) AND MASCARA) <> 0 THEN
  OUT C2, 5: WAIT PPI, &H20: WAIT PPI, &H20, &H20
  OUT C2, 13: WAIT PPI, &H20: WAIT PPI, &H20, &H20
  OUT C2, 5
 END IF
MASCARA = MASCARA \ 2
NEXT I
OUT B2, 67: WAIT PPI, &H20: WAIT PPI, &H20, &H20
OUT C2, 1: WAIT PPI, &H20: WAIT PPI, &H20, &H20
OUT C2, 9: WAIT PPI, &H20: WAIT PPI, &H20, &H20
OUT C2, 1
OUT B2, 3
OUT PPI, INP(PPI) AND &HFC 'desliga o som
STI& = &HCAFB&: DEF SEG = VARSEG(STI&): CALL ABSOLUTE(VARPTR(STI&)) 'habilita interrupcoes
END SUB

SUB SETZERO
CALL SETFINO(0)
LIM = 32000 / mT2FINO
CALL GETHALL: CAMPO = HALL * HALL2mT
IF ABS(CAMPO) > 3 * LIM THEN
 GROSSO = SGN(CAMPO) * INT(3 * LIM * mT2GROSSO + 3 * LIM * 3 * LIM * mT2GROSSO2)
 CALL SETGROSSO(GROSSO)
END IF
CALL LEHALL: CAMPO = HALL * HALL2mT: CAMPO2 = CAMPO
WHILE ABS(CAMPO) > LIM
 HALLANT = HALL
 CAMPO2 = CAMPO2 - SGN(CAMPO2) * .5 * LIM
 GROSSO = INT(CAMPO2 * mT2GROSSO + CAMPO2 * CAMPO2 * mT2GROSSO2)
 CALL SETGROSSO(GROSSO)
 CALL GETHALL
 LOCATE 12, 1: PRINT USING "H [mT]: +####.##ñ###.##   "; HALL * HALL2mT; ABS(HALL - HALLANT) * HALL2mT;
 CAMPO = HALL * HALL2mT
WEND
CALL LEHALL: CORRECAO = INT(-.7 * HALL * HALL2mT * mT2FINO)
WHILE ABS(HALL) > 1 AND ABS(CORRECAO) < 32700
 CALL SETFINO(CORRECAO)
 CALL LEHALL: CORRECAO = CORRECAO + INT(-.7 * HALL * HALL2mT * mT2FINO)
WEND
END SUB

FUNCTION TO360 (ANG)
  'Traz um angulo qualquer para o intervalo de 0 a 360 graus.
VOLTAS = INT(ANG / 360)
TO360 = ANG - 360 * VOLTAS
END FUNCTION

FUNCTION TXT$ (N)
  'Converte um numero em texto sem os brancos do inicio e do final.
TXT$ = RIGHT$(STR$(N), LEN(STR$(N)) - 1 - INT(SGN(N) / 2))
END FUNCTION

FUNCTION XPIX (N)
  'Levando em consideracao os atuais VIEW e WINDOW, retorna em unidades de X
  'o tamanho ocupado por N pixels.
XPIX = PMAP(N, 2) - PMAP(0, 2)
END FUNCTION

SUB XYZTODIM (X, Y, Z, DEC, INC, M)
  'Dados: X, Y e Z, calcula a declinacao, a inclinacao (em graus) e o modulo
  'do vetor correspondente.
IF X > 0 AND Y >= 0 THEN DEC = ATN(Y / X) * RG
IF X > 0 AND Y < 0 THEN DEC = 360 + ATN(Y / X) * RG
IF X = 0 THEN DEC = 0
IF X < 0 THEN DEC = 180 + ATN(Y / X) * RG
H = SQR(X * X + Y * Y)
IF H = 0 THEN INC = SGN(Z) * 90 ELSE INC = ATN(Z / H) * RG
M = SQR(X * X + Y * Y + Z * Z)
END SUB

FUNCTION YPIX (N)
  'Levando em consideracao os atuais VIEW e WINDOW, retorna em unidades de Y
  'o tamanho ocupado por N pixels.
YPIX = PMAP(N, 3) - PMAP(0, 3)
END FUNCTION

